buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.14.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: 'release-tools.gradle'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

def Properties localProps = new Properties()
localProps.load(new FileInputStream(file('../local.properties')))

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    def Properties keyProps = new Properties()
    assert localProps['keystore.props.file'];
    keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))

    signingConfigs {
        debug {
            storeFile file(keyProps['debugStoreFile'])
            storePassword keyProps['debugStorePassword']
            keyAlias keyProps['debugKeyAlias']
            keyPassword keyProps['debugKeyPassword']
        }
        release {
            storeFile file(keyProps['releaseStoreFile'])
            storePassword keyProps['releaseStorePassword']
            keyAlias keyProps['releaseKeyAlias']
            keyPassword keyProps['releaseKeyPassword']
        }
    }

    defaultConfig {
        applicationId 'com.apkola.muzei.lastfm'
        minSdkVersion 17
        targetSdkVersion 21

        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        signingConfig signingConfigs.release

        buildConfigField 'String', 'LASTFM_API_KEY', localProps['lastfm.api.key']
        buildConfigField 'String', 'LASTFM_DEFAULT_USERNAME', localProps['lastfm.default.username']
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            signingConfig signingConfigs.debug
        }
        alpha {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
        }
        beta {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
        }
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
        }
    }
}

dependencies {
    compile 'com.squareup.retrofit:retrofit:1.8.0'
    compile 'com.google.android.apps.muzei:muzei-api:1.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.1.0@aar') {
        transitive = true;
    }
}